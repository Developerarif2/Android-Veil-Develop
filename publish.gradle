apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply from: '../dependencies.gradle'

ext.repository = [
    name       : "androidveil",
    description: "An easy, flexible way to implement veil skeletons and shimmering effect for Android.",
    websiteUrl : "https://github.com/skydoves/AndroidVeil",
    scm        : 'scm:git@github.com/skydoves/AndroidVeil.git'
]

bintray {
    user = project.hasProperty("bintray.user") ? project.property("bintray.user") : ""
    key = project.hasProperty("bintray.key") ? project.property("bintray.key") : ""

    publications = ["release"]
    publish = true
    override = true
    dryRun = false

    pkg {
        repo = "maven"
        name = repository.name
        userOrg = "devmagician"
        desc = repository.description
        websiteUrl = repository.websiteUrl
        vcsUrl = "${websiteUrl}.git"
        issueTrackerUrl = "${websiteUrl}/issues"
        licenses = ["Apache-2.0"]
        labels = ["android"]
        publicDownloadNumbers = false

        githubRepo = websiteUrl
        githubReleaseNotesFile = "${websiteUrl}/releases"

        version {
            desc = repository.description
            name = versions.versionName
            vcsTag = versions.versionName
            released = new Date()
            gpg { sign = true }
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                group = "com.github.skydoves"
                artifactId = repository.name
                version = versions.versionName

                artifact androidJavadocsJar
                artifact(sourceJar)

                pom {
                    name = repository.name
                    description = repository.description
                    url = repository.websiteUrl
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "skydoves"
                            name = "Jaewgoon Eum"
                        }
                    }
                    scm {
                        connection = repository.scm
                        developerConnection = repository.scm
                        url = repository.websiteUrl
                    }
                }
            }
        }
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }
    exclude '**/R.html', '**/R.*.html', '**/index.html'
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}